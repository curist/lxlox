let lib = import "src/lib.lx"

let types = {}


fn enumNameMapper(enum) {
  let enumKeys = keys(enum)
  let mapping = {}
  for let i = 0; i < len(enumKeys); i = i + 1 {
    mapping[enum[enumKeys[i]]] = enumKeys[i]
  }
  return mapping
}

types.TOKEN = {
  let iota
  {
    LEFT_PAREN: {
      iota = lib.iota(100)
      iota()
    },
    RIGHT_PAREN: iota(),
    LEFT_BRACE: iota(),
    RIGHT_BRACE: iota(),
    COMMA: iota(),
    DOT: iota(),
    MINUS: iota(),
    PLUS: iota(),
    SEMICOLON: iota(),
    SLASH: iota(),
    STAR: iota(),
    COLON: iota(),
    LEFT_BRACKET: iota(),
    RIGHT_BRACKET: iota(),
    MOD: iota(),

    // One or two character tokens.
    BANG: {
      iota = lib.iota(200)
      iota()
    },
    BANG_EQUAL: iota(),
    EQUAL: iota(),
    EQUAL_EQUAL: iota(),
    GREATER: iota(),
    GREATER_EQUAL: iota(),
    LESS: iota(),
    LESS_EQUAL: iota(),

    // Literals.
    IDENTIFIER: {
      iota = lib.iota(300)
      iota()
    },
    STRING: iota(),
    NUMBER: iota(),

    // Keywords.
    AND: {
      iota = lib.iota(400) 
      iota()
    } ,
    OR: iota(),
    IF: iota(),
    ELSE: iota(),
    FN: iota(),
    FOR: iota(),
    NIL: iota(),
    RETURN: iota(),
    TRUE: iota(),
    FALSE: iota(),
    LET: iota(),
    BREAK: iota(),
    DEFER: iota(),
    IMPORT: iota(),

    // Termination
    ERROR: 990,
    EOF: 999,
  }
}
types.TOKEN_NAME = enumNameMapper(types.TOKEN)

types.KEYWORDS = {
  let tokenKeys = keys(types.TOKEN)
  let mapping = {}
  for let i = 0; i < len(tokenKeys); i = i + 1 {
    let tokenKey = tokenKeys[i]
    let enum = types.TOKEN[tokenKey]
    if enum >= 400 and enum < 500 {
      mapping[Lx.toLowerCase(tokenKey)] = true
    }
  }
  mapping
}

types
